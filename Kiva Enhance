desc:Kiva Enhance

slider1:cutoff=0.8<0.5,0.99>Cutoff
slider2:topend=0<0,0.5>Top End
slider3:topendDrive=1<1,30>Top End Drive
slider4:solo=0<0,1,1{Mix,Difference}>Solo
slider5:gain_db=0<-12,0>Makeup (dB)

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init
last_gain=10^(gain_db/20);

last_lpf0 = 0;
last_lpf1 = 0;

function saturate(sample) (
//  ((atan(topendDrive*sample) / (topendDrive^0.5)) / ($pi/2));
  ((atan(topendDrive*sample) / (topendDrive^0.7)) / ($pi/2));
);

@slider
next_gain=10^(gain_db/20);

@block
d_gain = (next_gain - last_gain)/samplesblock;

@sample
real_cutoff = cutoff ^ 4;

last_lpf0 += (spl0 - last_lpf0) * real_cutoff;
last_lpf1 += (spl1 - last_lpf1) * real_cutoff;

lpf0 = last_lpf0;
lpf1 = last_lpf1;

hpf0 = spl0;
hpf1 = spl1;

filter == 0 ? (
  hpf0 -= lpf0;
  hpf1 -= lpf1;

  hpf0 *= 1 + 3*(cutoff^3);
  hpf1 *= 1 + 3*(cutoff^3);
);

solo == 1 ? (
  spl0 = 2.5 * (topend * saturate(hpf0));
  spl1 = 2.5 * (topend * saturate(hpf1));
) : (
  spl0 = spl0 + (topend * saturate(hpf0));
  spl1 = spl1 + (topend * saturate(hpf1));
);

last_lpf0 = lpf0;
last_lpf1 = lpf1;

spl0 *= last_gain;
spl1 *= last_gain;

last_gain += d_gain;
